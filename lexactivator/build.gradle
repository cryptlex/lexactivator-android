plugins {
	id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 31
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    namespace 'com.cryptlex.android.lexactivator'
}

dependencies {
    implementation 'net.java.dev.jna:jna:5.13.0@aar'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.14.2'
}

if (project.hasProperty("lexVersion")) {
        project.setVersion(lexVersion)
}

ext {
    PUBLISH_GROUP_ID = 'com.cryptlex.android.lexactivator'
    PUBLISH_ARTIFACT_ID = 'lexactivator'
    PUBLISH_VERSION = version
    OSSRH_USERNAME = System.getenv('OSSRH_USERNAME')
    OSSRH_PASSWORD = System.getenv('OSSRH_PASSWORD')
}

nexusPublishing {
    repositories {
		sonatype {
			stagingProfileId = "24cc48a79ab9aa"
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
			username = OSSRH_USERNAME
            password = OSSRH_PASSWORD
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION
            afterEvaluate {
                from components.release
                pom {
                    name = 'LexActivator'
                    description = 'LexActivator wrapper for Android (licensing library).'
                    url = 'https://github.com/cryptlex/lexactivator-android'
                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://www.opensource.org/licenses/mit-license.php'
                        }
                    }
                    organization {
                        name = 'Cryptlex, LLC'
                        url = 'https://cryptlex.com'
                    }
                    developers {
                        developer {
                            name = 'Cryptlex Team'
                            email = 'support@cryptlex.com'
                            organization = 'Cryptlex, LLC.'
                            organizationUrl = 'https://cryptlex.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/cryptlex/lexactivator-android.git'
                        developerConnection = 'scm:git:ssh://github.com/cryptlex/lexactivator-android.git'
                        url = 'https://github.com/cryptlex/lexactivator-android'
                    }
                }
            }
        }

        test(MavenPublication) {
            groupId PUBLISH_GROUP_ID
            artifactId PUBLISH_ARTIFACT_ID
            version PUBLISH_VERSION
            afterEvaluate {
                from components.release
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.release
}
